module sdf2imp/services/create-menus-descriptor

imports
  libstratego-lib
  
  sdf2imp/util/-
  sdf2imp/services/common

strategies

  create-menus-descriptor =
    output-descriptor-file(
      <descriptor-name> "-Menus"
    ,
!${module {<descriptor-name> "-Menus"}

menus
  
  menu: "Syntax"                     (openeditor)
    
    action: "Format"               = editor-format (realtime) (source)
    action: "Show abstract syntax" = debug-show-aterm (realtime) (meta) (source)
    action: "Show analyzed syntax" = debug-show-analyzed (meta)
    
  menu: "Analysis"                   (meta)
    
    action: "Reset and reanalyze"  = debug-reanalyze  

    submenu: "Show analysis"         (openeditor)
      action: "Project"            = debug-project
      action: "Partition"          = debug-partition
    end
    
    submenu: "Show index"            (openeditor)
      action: "Project"            = debug-index-project
      action: "Project (resolved)" = debug-index-project-resolve
      action: "Partition"          = debug-index-partition
    end
    
    submenu: "Show tasks"            (openeditor)
      action: "Project"            = debug-task-project
      action: "Partition"          = debug-task-partition
      action: "Selection"          = debug-task-selection
    end
  
  menu: "Generation"                 (openeditor) (realtime)
    
    action: "Generate Java"        = generate-java

}
   ,
     not(file-exists)
   );
    
    create-derived-builder-descriptor
  
  // for backwards compatibility
  create-derived-builder-descriptor =
    sdf-name := <get-sdf-main-module>;
    if is-descriptor-imported(|${{sdf-name}-Builders}) then
      <output-text-file(|["editor"], ${{sdf-name}-Builders.generated.esv})>
      
${module {sdf-name}-Builders.generated

  // 
  // DEPRECATED. 
  //
  // Builders have been replaced by menus and actions (see 'editor/{sdf-name}-Menus.esv'). 
  // Providers, the observer and the on-save builder are now part of the main language description. Fix: move them to 'editor/{sdf-name}.main.esv'.
  // 
  
}
    end
